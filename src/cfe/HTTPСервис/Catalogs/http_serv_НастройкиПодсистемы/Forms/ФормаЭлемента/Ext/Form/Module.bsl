
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		
		Данные = Объект.Ссылка.СтатичныеДанные.Получить();
		
		Если Данные <> Неопределено Тогда
			АдресВХранилище = ПоместитьВоВременноеХранилище(Данные, ЭтаФорма.УникальныйИдентификатор);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьКФайлуНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ВыбратьФайл();
КонецПроцедуры


&НаКлиенте
Процедура ВыбратьФайл()
	
	Оповещение = Новый ОписаниеОповещения("ПодключитьРасширениеРаботыСФайламиЗавершение", ЭтотОбъект);
	НачатьПодключениеРасширенияРаботыСФайлами(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьРасширениеРаботыСФайламиЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Не Подключено Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Для продолжения необходимо установить расширение для работы с 1С:Предприятием.'"));
		Возврат;
	КонецЕсли;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'Архив '")+ "(*.zip)|*.zip";
	
	ПараметрыОповещения = Новый Структура;
	
	ОповещениеВыбора = Новый ОписаниеОповещения("ВыбранФайл", ЭтотОбъект, ПараметрыОповещения);
	ДиалогОткрытияФайла.Показать(ОповещениеВыбора);
	
КонецПроцедуры


&НаКлиенте
Процедура ВыбранФайл(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПутьКФайлу = ВыбранныеФайлы[0];
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьZIPАрхив(Команда)
	
	Если ПустаяСтрока(ПутьКФайлу) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "ПутьКФайлу не заполнен, прервано";
		Сообщение.Сообщить(); 
		Возврат;
	КонецЕсли;
	
	Файл = Новый ДвоичныеДанные(ПутьКФайлу);
	АдресВХранилище = ПоместитьВоВременноеХранилище(Файл, ЭтаФорма.УникальныйИдентификатор);
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ПустаяСтрока(АдресВХранилище) Тогда
		ТекущийОбъект.СтатичныеДанные = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(АдресВХранилище));
	Иначе
		ТекущийОбъект.СтатичныеДанные = Новый ХранилищеЗначения(Неопределено);
	КонецЕсли;
	
КонецПроцедуры
