Функция ОбработатьЗапрос(Запрос) Экспорт
	
	СтрокаЗапроса = Запрос.ОтносительныйURL;
	
	МассивАдресЗапроса = СтрРазделить(СтрокаЗапроса, "/");
	НаименованиеУзла = МассивАдресЗапроса[1];
	Если ПустаяСтрока(НаименованиеУзла) Тогда
		Возврат СформироватьОтвет(404, СтрокаЗапроса);	
	КонецЕсли;
	
	Узел = http_serv_ПовторноеИспользование.ПолучитьУзел(НаименованиеУзла);
	
	Если Не ЗначениеЗаполнено(Узел) Тогда
		Возврат СформироватьОтвет(404, СтрокаЗапроса);
	КонецЕсли;
	
	ТаблицаСтатическихДанных = http_serv_ПовторноеИспользование.ПолучитьТаблицуСтатическихДанных(НаименованиеУзла);
	
	СтрокаЗапросаСокр = Сред(СтрокаЗапроса, СтрДлина(НаименованиеУзла) + 3); 
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ИмяФайла", СтрокаЗапросаСокр);
	
	
	нСтроки = ТаблицаСтатическихДанных.НайтиСтроки(ПараметрыОтбора);
	
	Если нСтроки.Количество() = 0 Тогда
		Возврат СформироватьОтвет(404, СтрокаЗапросаСокр);
	КонецЕсли;
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(нСтроки[0].АдресВХранилище);
	
	Возврат СформироватьОтвет(200, ДвоичныеДанные);
	
КонецФункции

Функция СформироватьОтвет(КодСостояния = 200, Данные = "")
	
	Ответ = Новый HTTPСервисОтвет(КодСостояния);
	
	Если ТипЗнч(Данные) = Тип("Строка") Тогда
		Ответ.Заголовки.Вставить("Content-Type", "application/json");	
		Ответ.УстановитьТелоИзСтроки(Данные);
	Иначе
		
		Ответ.УстановитьТелоИзДвоичныхДанных(Данные);
	КонецЕсли;
	Возврат Ответ;
	
КонецФункции

